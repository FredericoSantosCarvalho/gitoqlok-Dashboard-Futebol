///$tab Main
//Let vL.LibIncCarga	=  'https://codespaces.new/FredericoSantosCarvalho/Qlik-Versionamento-Futebol';
// Let vL.LibIncCarga	= 'lib://FredericoSantosCarvalho/Qlik-Versionamento-Futebol';

SET ThousandSep='.';
SET DecimalSep=',';
SET MoneyThousandSep='.';
SET MoneyDecimalSep=',';
SET MoneyFormat='R$#.##0,00;-R$#.##0,00';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD/MM/YYYY';
SET TimestampFormat='DD/MM/YYYY hh:mm:ss[.fff]';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='pt-BR';
SET CreateSearchIndexOnReload=1;
SET MonthNames='jan;fev;mar;abr;mai;jun;jul;ago;set;out;nov;dez';
SET LongMonthNames='janeiro;fevereiro;março;abril;maio;junho;julho;agosto;setembro;outubro;novembro;dezembro';
SET DayNames='seg;ter;qua;qui;sex;sáb;dom';
SET LongDayNames='segunda-feira;terça-feira;quarta-feira;quinta-feira;sexta-feira;sábado;domingo';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:μ;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';



/*
Let vL.LibIncCarga	= 'lib://11.GTT/3.Include/6.Custom/3.Load/GALS';

$(Must_Include=$(vL.LibIncCarga)\1.L_CGLs_Data_Analisys.inc);
*/


//LIB CONNECT TO 'Futebol:GitHub Versinamento Futebol';


///$tab Includes
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/00.00 - Main.qvs);
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/01.00 - Rotinas.inc);
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/02.00 - EX Tabelas.inc);
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/03.00 - TR Fato Futebol.inc);
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/03.01 - TR Dim Artilheiro.inc);
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/03.02 - TR Dim Caldendário.inc);
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/03.03 - TR Dim Escalacoes.inc);
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/04.00 - Load Tabelas.inc);
$(Must_Include=lib://Futebol:G_DRIVE_SNUNES_BIX/1-0YSPTmpIN3Uj63ibK3ipTj5dxZwQLg9/04.01 - Variáveis.inc);

// Teste alteracao

TRACE ---- TESTE TESTE TESTE;

// NOVA ALTERACAO NO BLOCO INCLUDES
Trace --- ALTERACAO INCLUDES;

Trace --- TESTE PEDRO;

Exit Script;

///$tab 01.00 - Rotinas
TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Variáveis Básicas Real'
-------------------------------------------------------------;

Sub TratarExpansaoMacro(vNomeVariavel)
    Let vNomeVariavel = Text(Replace(vNomeVariavel, '@', '$'));
End Sub;


///$tab 02.00 - EX Tabelas
TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Início da Extração'
-------------------------------------------------------------;




EX_Brasileirao_Serie_A:
LOAD
    ano_campeonato,
    data,
    rodada,
    estadio,
    arbitro,
    publico,
    publico_max,
    time_mandante,
    time_visitante,
    tecnico_mandante,
    tecnico_visitante,
    colocacao_mandante,
    colocacao_visitante,
    valor_equipe_titular_mandante,
    valor_equipe_titular_visitante,
    idade_media_titular_mandante,
    idade_media_titular_visitante,
    gols_mandante,
    gols_visitante,
    gols_1_tempo_mandante,
    gols_1_tempo_visitante,
    escanteios_mandante,
    escanteios_visitante,
    faltas_mandante,
    faltas_visitante,
    chutes_bola_parada_mandante,
    chutes_bola_parada_visitante,
    defesas_mandante,
    defesas_visitante,
    impedimentos_mandante,
    impedimentos_visitante,
    chutes_mandante,
    chutes_visitante,
    chutes_fora_mandante,
    chutes_fora_visitante
FROM [lib://Futebol:DataFiles/brasileirao_serie_a.csv]
(txt, utf8, embedded labels, delimiter is ',', msq);

STORE EX_Brasileirao_Serie_A INTO [lib://Futebol:DataFiles/EX_Brasileirao_Serie_A.qvd](qvd);
DROP TABLE EX_Brasileirao_Serie_A;



EX_Artilheiro:
LOAD
    ANO,
    VENCEDOR,
    EQUIPE,
    GOLS
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Artilheiros);

STORE EX_Artilheiro INTO [lib://Futebol:DataFiles/EX_Artilheiro.qvd](qvd);
DROP TABLE EX_Artilheiro;



EX_Escalacoes:
LOAD
    "Time",
    Formação,
    Goleiro,
    Defesa,
    "Meio-campo",
    Ataque,
    Ano
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes);

STORE EX_Escalacoes INTO [lib://Futebol:DataFiles/EX_Escalacoes.qvd](qvd);
DROP TABLE EX_Escalacoes;


///$tab 03.00 - TR Fato Futebol
TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Início da Transformção'
-------------------------------------------------------------;

TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Fato Futebol'
-------------------------------------------------------------;

TR_Fato_Futebol:
LOAD
	time_mandante											as %Time_Image,
    time_mandante&ano_campeonato							as %Time,
    ano_campeonato											as %Ano,	
    ano_campeonato											as %AnoEscalacoes,
    data													as %Data,
    time_mandante											as Time,
    /***************************************************************************************
    If para cálculo das vitórias, derrotas e empates dos times mandantes	
    ***************************************************************************************/    
    IF( gols_mandante>gols_visitante
    	,'V'
        ,IF(	gols_visitante=gols_mandante
        		,'E'
                ,'D'
        )
	    
    )														as [Status],
    /***************************************************************************************
    If para cálculo da pontuação dos times mandantes
    ***************************************************************************************/
    IF( gols_mandante>gols_visitante
    	,3
        ,IF(	gols_visitante=gols_mandante
        		,1
                ,0
        )
	    
    )														as [Pts],    
    'Mandante'												as [Status Time],
    rodada													as [Rodada],
    estadio													as [Estádio],
    arbitro													as [Arbitro],
    publico													as [Púlico],
    publico_max												as [Público Max],
    tecnico_mandante										as [Técnico],
    colocacao_mandante										as [Colocação],
    valor_equipe_titular_mandante							as [Valor Equipe],
    idade_media_titular_mandante							as [Idade Média],
    gols_mandante											as [Gols Geral],
    gols_mandante											as [Gols Feitos],
    gols_visitante											as [Gols Sofridos],
    gols_1_tempo_mandante									as [Gols 1 Tempo],
    escanteios_mandante										as [Escanteio],
    faltas_mandante											as [Faltas],
    chutes_bola_parada_mandante								as [Chute Bola Parada],
    defesas_mandante										as [Defesas],
    impedimentos_mandante									as [Impedimentos],
    chutes_mandante											as [Chutes],
    chutes_fora_mandante									as [Chutes fora]
FROM [lib://Futebol:DataFiles/EX_Brasileirao_Serie_A.qvd]
(qvd);


Concatenate(TR_Fato_Futebol)

LOAD
	time_visitante											as %Time_Image,
    time_visitante&ano_campeonato							as %Time,
    ano_campeonato											as %Ano,
    ano_campeonato											as %AnoEscalacoes,
    data													as %Data,
    time_visitante											as Time,
    /***************************************************************************************
    If para cálculo das vitórias, derrotas e empates dos times visitantes	
    ***************************************************************************************/    
    IF( gols_visitante>gols_mandante
    	,'V'
        ,IF(	gols_visitante=gols_mandante
        		,'E'
                ,'D'
        )
	    
    )														as [Status],
    /***************************************************************************************
    If para cálculo da pontuação dos times visitantes	
    ***************************************************************************************/    
    IF( gols_visitante>gols_mandante
    	,3
        ,IF(	gols_visitante=gols_mandante
        		,1
                ,0
        )
	    
    )														as [Pts],    
    'Visitante'												as [Status Time],    
    rodada													as [Rodada],
    estadio													as [Estádio],
    arbitro													as [Arbitro],
    publico													as [Público],
    publico_max												as [Público Max],
    tecnico_visitante										as [Tecnico],
    colocacao_visitante										as [Colocação],
    valor_equipe_titular_visitante							as [Valor Equipe],
    idade_media_titular_visitante							as [Idade Média],
    gols_visitante											as [Gols Geral],
    gols_visitante											as [Gols Feitos],
    gols_mandante											as [Gols Sofridos],
    gols_1_tempo_visitante									as [Gols 1 Tempo],
    escanteios_visitante									as [Escanteio],
    faltas_visitante										as [Faltas],
    chutes_bola_parada_visitante							as [Chute Bola Parada],
    defesas_visitante										as [Defesas],
    impedimentos_visitante									as [Impedimentos],
    chutes_visitante										as [Chutes],
    chutes_fora_visitante									as [Chutes fora]
FROM [lib://Futebol:DataFiles/EX_Brasileirao_Serie_A.qvd]
(qvd);


STORE TR_Fato_Futebol INTO [lib://Futebol:DataFiles/TR_Fato_Futebol.qvd](qvd);

///$tab 03.01 - TR Dim Artilheiro
TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Dim Artilheiro'
-------------------------------------------------------------;

TR_Dim_Artilheiro:
LOAD
    ANO														as %Ano,
    VENCEDOR												as %Artilheiro,
    EQUIPE													as Equipe,
    GOLS													as Gols
FROM [lib://Futebol:DataFiles/EX_Artilheiro.qvd]
(qvd);


STORE TR_Dim_Artilheiro INTO [lib://Futebol:DataFiles/TR_Dim_Artilheiro.qvd](qvd);
DROP TABLE TR_Dim_Artilheiro;
///$tab 03.02 - TR Dim Caldendário
TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Dim Calendário'
-------------------------------------------------------------;
Temp:  
Load  
	min(%Data) 												as minDate,  
	max(%Data) 												as maxDate  
Resident TR_Fato_Futebol;  
      
Let varMinDate = Num(Peek('minDate', 0, 'Temp'));  
Let varMaxDate = Num(Peek('maxDate', 0, 'Temp'));  
DROP Table Temp;  

//Gera todas as datas do mínimo ao máximo
TempCalendar:  
LOAD  
	$(varMinDate) + Iterno()-1 								as Num,  
	Date($(varMinDate) + IterNo() - 1) 						as TempDate  
AutoGenerate 1 While $(varMinDate) + IterNo() -1 <= $(varMaxDate);  

//Criar o calendário através das funções mínimo e máximo
TR_Dim_Caldendario:  
Load  
  	TempDate 												as %Data,  
  	week(TempDate) 											as Semana,  
  	Year(TempDate) 											as Ano,  
  	Month(TempDate) 										as Mês,  
  	Day(TempDate) 											as Dia,  
  	YearToDate(TempDate)*-1									as CurYTDFlag,
  	YearToDate(TempDate,-1)*-1								as LastYTDFlag,
  	Date(MonthStart(TempDate),'MM/YYYY')					as [Mês Ano]
Resident TempCalendar  
Order By TempDate ASC;  
Drop Table TempCalendar;  


STORE TR_Dim_Caldendario INTO [lib://Futebol:DataFiles/TR_Dim_Caldendario.qvd](qvd);
DROP TABLES TR_Dim_Caldendario, TR_Fato_Futebol;
///$tab 03.03 - TR Dim Escalacoes
TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Dim Escalações'
-------------------------------------------------------------;

Dim_Escalacoes:
/*********************************************************************************************
Tratamento das escalações, apenas os casos dos zagueiros, quanto tem 3 na posição. Ex: 3-4-3 	
*********************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField(Defesa,',',1)			as Zn1, 
    SubField(Defesa,',',2)			as Zn2,
    SubField(Defesa,',',3)			as Zn3
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',1)	= 3
;

Concatenate
/*********************************************************************************************
Tratamento das escalações, apenas os casos dos zagueiros, quanto tem 4 na posição. Ex: 4-3-3 	
*********************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField(Defesa,',',1)			as Zn1, 
    SubField(Defesa,',',2)			as Zn2,
    SubField(Defesa,',',3)			as Zn3,
    SubField(Defesa,',',4)			as Zn4
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',1)	= 4
;

Concatenate
/*********************************************************************************************
Tratamento das escalações, apenas os casos dos zagueiros, quanto tem 5 na posição. Ex: 5-3-2 	
*********************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField(Defesa,',',1)			as Zn1, 
    SubField(Defesa,',',2)			as Zn2,
    SubField(Defesa,',',3)			as Zn3,
    SubField(Defesa,',',4)			as Zn4,
    SubField(Defesa,',',5)			as Zn5    
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',1)	= 5
;

Concatenate

/*********************************************************************************************
Tratamento das escalações, apenas os casos dos Meio, quanto tem 4 na posição. Ex: 4-3-3 	
*********************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField([Meio-campo],',',1)	as Meio1, 
    SubField([Meio-campo],',',2)	as Meio2,
    SubField([Meio-campo],',',3)	as Meio3
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',2)	= 3  
;

Concatenate

/*********************************************************************************************
Tratamento das escalações, apenas os casos dos Meio, quanto tem 5 na posição. Ex: 4-4-2 	
*********************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField([Meio-campo],',',1)	as Meio1, 
    SubField([Meio-campo],',',2)	as Meio2,
    SubField([Meio-campo],',',3)	as Meio3,
    SubField([Meio-campo],',',4)	as Meio4
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',2)	= 4  
;

Concatenate

/*********************************************************************************************
Tratamento das escalações, apenas os casos dos Meio, quanto tem 4 na posição. Ex: 3-5-2 	
*********************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField([Meio-campo],',',1)	as Meio1, 
    SubField([Meio-campo],',',2)	as Meio2,
    SubField([Meio-campo],',',3)	as Meio3,
    SubField([Meio-campo],',',4)	as Meio4,
    SubField([Meio-campo],',',5)	as Meio5
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',2)	= 5
;

Concatenate

/*********************************************************************************************
Tratamento das escalações, apenas os casos dos Atacante, quanto tem 1 na posição. Ex: 4-5-1 	
****+****************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField([Ataque],',',1)		as Ataque1
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',3)	= 1
;

Concatenate
/*********************************************************************************************
Tratamento das escalações, apenas os casos dos Atacante, quanto tem 4 na posição. Ex: 4-4-2 	
****+****************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField([Ataque],',',1)		as Ataque1, 
    SubField([Ataque],',',2)		as Ataque2
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',3)	= 2
;

Concatenate

/*********************************************************************************************
Tratamento das escalações, apenas os casos dos Atacante, quanto tem 4 na posição. Ex: 4-3-3 	
****+****************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField([Ataque],',',1)		as Ataque1, 
    SubField([Ataque],',',2)		as Ataque2,
    SubField([Ataque],',',3)		as Ataque3
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	SubField([Formação],'-',3)	= 3
;


Concatenate



/*********************************************************************************************
Tratamento das escalações, apenas os casos dos Goleiro. 	
****+****************************************************************************************/ 
LOAD
    "Time"							as [Time Ilha],
   	SubField([Goleiro],',',1)		as Go
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes)
Where 
 	Len(Goleiro) > 0
;

TR_Ilha_Escalacoes:
CrossTable(Pos,Jogadores,1)
Load
	[Time Ilha],
    Zn1,
    Zn2,
    Zn3,
    Zn4,
    Zn5,
    Meio1,
	Meio2,
	Meio3,
	Meio4,
	Meio5,
    Ataque1, 
	Ataque2,
	Ataque3,
    Go  
Resident Dim_Escalacoes;

Drop Table Dim_Escalacoes;


Left Join
LOAD
    [Time]							as [Time Ilha],
    Text(Formação)					as [Formação],
    2022							as [%AnoEscalacoes],
    SubField([Formação],'-',1)		as Zn,
    SubField([Formação],'-',2)		as Meio,
    SubField([Formação],'-',3)		as Ata
FROM [lib://Futebol:DataFiles/brasileirao.xlsx]
(ooxml, embedded labels, table is Escalacoes);


STORE TR_Ilha_Escalacoes INTO [lib://Futebol:DataFiles/TR_Ilha_Escalacoes.qvd](qvd);
DROP TABLE TR_Ilha_Escalacoes;


///$tab 04.00 - Load Tabelas
TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Início da Carga'
-------------------------------------------------------------;

Fato_Futebol:
LOAD
    %Time_Image,
    %Time,
    %Ano,
    %AnoEscalacoes,
    %Data,
    "Time",
    Status,
    Pts,
    "Status Time",
    Rodada,
    Estádio,
    Arbitro,
    Púlico,
    "Público Max",
    Técnico,
    Colocação,
    "Valor Equipe",
    "Idade Média",
    "Gols Geral",
    "Gols Feitos",
    "Gols Sofridos",
    "Gols 1 Tempo",
    Escanteio,
    Faltas,
    "Chute Bola Parada",
    Defesas,
    Impedimentos,
    Chutes,
    "Chutes fora",
    Público,
    Tecnico
FROM [lib://Futebol:DataFiles/TR_Fato_Futebol.qvd]
(qvd);

Dim_Artilheiro:
LOAD
    %Ano,
    %Artilheiro,
    Equipe,
    Gols
FROM [lib://Futebol:DataFiles/TR_Dim_Artilheiro.qvd]
(qvd);

Dim_Caldendario:
LOAD
    %Data,
    Semana,
    Ano,
    Mês,
    Dia,
    CurYTDFlag,
    LastYTDFlag,
    "Mês Ano"
FROM [lib://Futebol:DataFiles/TR_Dim_Caldendario.qvd]
(qvd);

TR_Ilha_Escalacoes:
LOAD
    "Time Ilha",
    "Pos",
    Jogadores,
    Formação,
    %AnoEscalacoes,
    Zn,
    Meio,
    Ata
FROM [lib://Futebol:DataFiles/TR_Ilha_Escalacoes.qvd]
(qvd);

///$tab 04.01 - Variáveis
TRACE
-------------------------------------------------------------
 > Iniciando Carga: 'Variáveis'
-------------------------------------------------------------;


//-------------------------------------------------------------------------------------
//		TRATAMENTO MEDIDAS DE TIMES
//-------------------------------------------------------------------------------------
Set v_Ano					= Max(Ano);


Set v_NumRodadas			= Max(Rodada);
Call TratarExpansaoMacro(v_NumRodadas);

Set v_Vitorias				= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'V'}>}Status);
Call TratarExpansaoMacro(v_Vitorias);

Set v_Derrotas				= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'D'}>}Status);
Call TratarExpansaoMacro(v_Derrotas);

Set v_Empates				= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'E'}>}Status);
Call TratarExpansaoMacro(v_Empates);

Set v_Pontuacao 			= 
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Pts]);
Call TratarExpansaoMacro(v_Pontuacao);

Set v_Chutes				= 
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Chutes]);
Call TratarExpansaoMacro(v_Chutes);

Set v_ChutesBolaParada		= 
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Chute Bola Parada]);
Call TratarExpansaoMacro(v_ChutesBolaParada);

Set v_ChutesFora			= 
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Chutes fora]);
Call TratarExpansaoMacro(v_ChutesFora);

Set v_Defesa				= 
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Defesas]);
Call TratarExpansaoMacro(v_Defesa);

Set v_Impedimentos			= 
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Impedimentos]);
Call TratarExpansaoMacro(v_Impedimentos);

Set v_Faltas				= 
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Faltas]);
Call TratarExpansaoMacro(v_Faltas);

Set v_Escanteio				= 
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Escanteio]);
Call TratarExpansaoMacro(v_Escanteio);

Set v_Idade_Media			=
Avg({<Rodada={"<=@(=Max(Rodada))"}>}[Idade Média]);
Call TratarExpansaoMacro(v_Idade_Media);

Set v_Valor_Equipe			=
Avg({<Rodada={"<=@(=Max(Rodada))"}>}[Valor Equipe]);
Call TratarExpansaoMacro(v_Valor_Equipe);

Set v_Total_Gols			=
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Gols Geral]);
Call TratarExpansaoMacro(v_Total_Gols);

Set v_Media_Publico			=
Avg({<Rodada={"<=@(=Max(Rodada))"}>}[Público]);
Call TratarExpansaoMacro(v_Media_Publico);

Set v_Total_Arbitros		=
Count(distinct{<Rodada={"<=@(=Max(Rodada))"}>} Arbitro);
Call TratarExpansaoMacro(v_Total_Arbitros);

Set v_Media_Gols			=
Avg({<Rodada={"<=@(=Max(Rodada))"}>}[Gols Geral]);
Call TratarExpansaoMacro(v_Media_Gols);

Set v_Gols_Sofridos			=
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Gols Sofridos]);
Call TratarExpansaoMacro(v_Gols_Sofridos);

Set v_Gols_Feitos			=
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Gols Feitos]);
Call TratarExpansaoMacro(v_Gols_Feitos);

Set v_Gols_Saldo			=
Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Gols Feitos]-[Gols Sofridos]);//-Sum({<Rodada={"<=@(=Max(Rodada))"}>}[Gols Sofridos]);
Call TratarExpansaoMacro(v_Gols_Saldo);

Set v_Total_Jogos_Estadio	=
Count({<Rodada={"<=@(=Max(Rodada))"},[Status Time]={'Mandante'}>}[Estádio]);
Call TratarExpansaoMacro(v_Total_Jogos_Estadio);

//-------------------------------------------------------------------------------------
//		TRATAMENTO MEDIDAS DE CLASSIFICAÇÃO TIMES
//-------------------------------------------------------------------------------------
Set v_Posicao_Time			=
Aggr(
        Rank(
            @(v_Pontuacao) +@(v_Vitorias)/100 + @(v_Gols_Saldo)/10000, 
            4, 
            1
        ), [Time]
);
Call TratarExpansaoMacro(v_Posicao_Time);




Set v_Classificacao_Time	=
IF(
    Aggr(
        Rank(
            @(v_Pontuacao) +@(v_Vitorias)/100 + @(v_Gols_Saldo)/10000, 
            4, 
            1
        ), Time
    ) <= 4, 
    'Libertadores',
    IF(
        Aggr(
            Rank(
                @(v_Pontuacao) + @(v_Vitorias)/100 + @(v_Gols_Saldo)/10000, 
                4, 
                1
            ), Time
        ) > 4 AND 
        Aggr(
            Rank(
                @(v_Pontuacao) + @(v_Vitorias)/100 + @(v_Gols_Saldo)/10000, 
                4, 
                1
            ), Time
        ) <= 6, 
        'Pré Libertadores',
        IF(
            Aggr(
                Rank(
                    @(v_Pontuacao) + @(v_Vitorias)/100 + @(v_Gols_Saldo)/10000, 
                    4, 
                    1
                ), Time
            ) > 6 AND 
            Aggr(
                Rank(
                    @(v_Pontuacao) + @(v_Vitorias)/100 + @(v_Gols_Saldo)/10000, 
                    4, 
                    1
                ), Time
            ) <= 12, 
            'Sulamericana',
            IF(
                Aggr(
                    Rank(
                         @(v_Pontuacao) + @(v_Vitorias)/100 + @(v_Gols_Saldo)/10000, 
                        4, 
                        1
                    ), Time
                ) > 12 AND 
                Aggr(
                    Rank(
                        @(v_Pontuacao) + @(v_Vitorias)/100 + @(v_Gols_Saldo)/10000, 
                        4, 
                        1
                    ), Time
                ) <= 16, 
                'Sem Competição',
                'Rebaixado'
            )
        )
    )
);
Call TratarExpansaoMacro(v_Classificacao_Time);





//-------------------------------------------------------------------------------------
//		TRATAMENTO TIME ILHA
//-------------------------------------------------------------------------------------
Set v_VitoriasIlha	= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'V'},Time={'@(v_Time_Ilha)'}>}Status);
Call TratarExpansaoMacro(v_VitoriasIlha);


Set v_DerrotasIlha			= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'D'},Time={'@(v_Time_Ilha)'}>}Status);
Call TratarExpansaoMacro(v_DerrotasIlha);


Set v_EmpatesIlha			= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'E'},Time={'@(v_Time_Ilha)'}>}Status);
Call TratarExpansaoMacro(v_EmpatesIlha);


Set v_PontuacaoIlha			= 
Sum({<Rodada={"<=@(=Max(Rodada))"},Time={'@(v_Time_Ilha)'}>}[Pts]);
Call TratarExpansaoMacro(v_PontuacaoIlha);


Set v_Valor_Equipe_Ilha		=
Num(Avg({<Rodada={"<=@(=Max(Rodada))"},Time={'@(v_Time_Ilha)'}>}[Valor Equipe])/1000000,'#.##0 M');
Call TratarExpansaoMacro(v_Valor_Equipe_Ilha);

Set v_Media_Publico_Ilha	=
Num(Avg({<Rodada={"<=@(=Max(Rodada))"},Time={'@(v_Time_Ilha)'}>}[Público])/1000,'#.##0 k');
Call TratarExpansaoMacro(v_Media_Publico_Ilha);

//-------------------------------------------------------------------------------------
//		TRATAMENTO TIME LEADER
//-------------------------------------------------------------------------------------
Set v_Time_Leader			= Aggr(
								Only({<Rodada={"<=@(=Max(Rodada))"},Colocação={1}>}Time)
                          		,Time,Rodada,Colocação
                        	);
Call TratarExpansaoMacro(v_Time_Leader);

Set v_VitoriasLeader		= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'V'},Time={'@(v_Time_Leader_Novo)'}>}Status);
Call TratarExpansaoMacro(v_VitoriasLeader);

Set v_DerrotasLeader		= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'D'},Time={'@(v_Time_Leader_Novo)'}>}Status);
Call TratarExpansaoMacro(v_DerrotasLeader);

Set v_EmpatesLeader			= 
Count({<Rodada={"<=@(=Max(Rodada))"},Status={'E'},Time={'@(v_Time_Leader_Novo)'}>}Status);
Call TratarExpansaoMacro(v_EmpatesLeader);

Set v_PontuacaoLeader		= 
Sum({<Rodada={"<=@(=Max(Rodada))"},Time={'@(v_Time_Leader_Novo)'}>}[Pts]);
Call TratarExpansaoMacro(v_PontuacaoLeader);

Set v_Valor_Equipe_Leader	=
Num(Avg({<Rodada={"<=@(=Max(Rodada))"},Time={'@(v_Time_Leader_Novo)'}>}[Valor Equipe])/1000000,'#.##0 M');
Call TratarExpansaoMacro(v_Valor_Equipe_Leader);

Set v_Media_Publico_Leader	=
Num(Avg({<Rodada={"<=@(=Max(Rodada))"},Time={'@(v_Time_Leader_Novo)'}>}[Público])/1000,'#.##0 k');
Call TratarExpansaoMacro(v_Media_Publico_Leader);



//-------------------------------------------------------------------------------------
//		TRATAMENTO DAS CORES
//-------------------------------------------------------------------------------------
/*
Set v_Cor_Fundo_Filtro			=	RGB(38,38,38);
Set v_Cor_Botao_Selecionado		=	RGB(38,38,38);
Set v_Cor_Botao					=	RGB(180,180,180);
Set v_Cor_Fundo_Geral			=	RGB(128,128,128);
*/
Set v_Cor_Resultado				=
IF(	@(v_Numero_Rodadas)*3+@(v_PontuacaoIlha)> @(v_PontuacaoLeader)
		,RGB(144,238,144)
    	,RGB(220,20,60)
)	;
Call TratarExpansaoMacro(v_Cor_Resultado);
Set v_Cor_Competicao			=
Pick(Wildmatch([Competição],'Rebaixado'		,'Libertadores'	  ,'Pré Libertadores'	,'Sulamericana'	,'Sem Competição')
						   ,RGB(220,20,60)	,RGB(176,224,230) ,RGB(141,179,187)		,RGB(255,165,0)	,RGB(211,211,211)
);





//-------------------------------------------------------------------------------------
//		TRATAMENTO DAS VARIAÇÕES
//-------------------------------------------------------------------------------------
Set v_Variacao_Vitoria			=
Num(((@(v_VitoriasIlha)-@(v_VitoriasLeader))/@(v_VitoriasLeader))*100,'#.##0,00');
Call TratarExpansaoMacro(v_Variacao_Vitoria);


Set v_Variacao_Empate			=
Num(((@(v_EmpatesIlha)-@(v_EmpatesLeader))/@(v_EmpatesLeader))*100,'#.##0,00');
Call TratarExpansaoMacro(v_Variacao_Empate);

Set v_Variacao_Derrota			=
Num(((@(v_DerrotasIlha)-@(v_DerrotasLeader))/@(v_DerrotasLeader))*100,'#.##0,00');
Call TratarExpansaoMacro(v_Variacao_Derrota);




//-------------------------------------------------------------------------------------
//		TRATAMENTO DO VISUAL ESCALAÇÕES NO MAPA
//-------------------------------------------------------------------------------------
Set v_Count_Formacao 			= GetSelectedCount([Formação]);
Set v_Count_Time 				= GetSelectedCount([Time Ilha]);
Set v_Formacao					= GetFieldSelections([Formação]);
Set v_Zn						= Subfield(GetFieldSelections([Formação]),'-',1);
Set v_Meio						= Subfield(GetFieldSelections([Formação]),'-',2);
Set v_Ataque					= Subfield(GetFieldSelections([Formação]),'-',3);
		
		
Set v_Goleiro					= ONLY({<Pos={'Go'}>}Jogadores);
Set v_Z1						= ONLY({<Pos={'Zn1'}>}Jogadores);
Set v_Z2						= ONLY({<Pos={'Zn2'}>}Jogadores);
Set v_Z3						= ONLY({<Pos={'Zn3'}>}Jogadores);
Set v_Z4						= ONLY({<Pos={'Zn4'}>}Jogadores);
Set v_Z5						= ONLY({<Pos={'Zn5'}>}Jogadores);
				
Set v_Meio1						= ONLY({<Pos={'Meio1'}>}Jogadores);
Set v_Meio2						= ONLY({<Pos={'Meio2'}>}Jogadores);
Set v_Meio3						= ONLY({<Pos={'Meio3'}>}Jogadores);
Set v_Meio4						= ONLY({<Pos={'Meio4'}>}Jogadores);
Set v_Meio5						= ONLY({<Pos={'Meio5'}>}Jogadores);
				
Set v_Ataque1					= ONLY({<Pos={'Ataque1'}>}Jogadores);
Set v_Ataque2					= ONLY({<Pos={'Ataque2'}>}Jogadores);
Set v_Ataque3					= ONLY({<Pos={'Ataque3'}>}Jogadores);